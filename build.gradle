buildscript {
  ext.versions = [
      'kotlin': '1.4.31',
      'dagger': '2.33',
      'incap' : '0.2',
  ]

  ext.deps = [
      'dagger': "com.google.dagger:dagger:${versions.dagger}",
      'daggerCompiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
      'autoService': 'com.google.auto.service:auto-service:1.0-rc4',
      'autoCommon': 'com.google.auto:auto-common:0.10',
      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.42',
      'kotlin': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      'javaPoet': 'com.squareup:javapoet:1.11.1',
      'guava': 'com.google.guava:guava:26.0-jre',
      'compileTesting': 'com.google.testing.compile:compile-testing:0.15',
      'robolectric': 'org.robolectric:robolectric:4.0-alpha-3',
      'inject': 'javax.inject:javax.inject:1',
      'android': 'com.google.android:android:4.1.1.4',
      'androidxAnnotations': 'androidx.annotation:annotation:1.0.0',
      'incap': [
          'runtime': "net.ltgt.gradle.incap:incap:${versions.incap}",
          'processor': "net.ltgt.gradle.incap:incap-processor:${versions.incap}",
      ],
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.android.tools.build:gradle:3.4.2'
  }

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  // Guava is depended on transitively. Force it to the same version to prevent missing methods.
  // This occurs when the compilation classpath picks 23.x whereas tests need 25.x.
  configurations.all {
    it.resolutionStrategy {
      it.force(deps.guava)
    }
  }

  group = GROUP
  version = VERSION_NAME

  plugins.withId('signing') {
    signing {
      def signingKey = findProperty('signingKey')
      def signingPassword = ''
      useInMemoryPgpKeys(signingKey, signingPassword)
    }
  }
}
